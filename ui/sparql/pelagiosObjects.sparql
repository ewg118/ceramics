PREFIX rdf:	<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX crm:	<http://www.cidoc-crm.org/cidoc-crm/>
PREFIX dcterms:	<http://purl.org/dc/terms/>
PREFIX foaf:	<http://xmlns.com/foaf/0.1/>
PREFIX kid:	<http://kerameikos.org/id/>
PREFIX kon:	<http://kerameikos.org/ontology#>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX skos:	<http://www.w3.org/2004/02/skos/core#>
PREFIX svcs: <http://rdfs.org/sioc/services#>
PREFIX edm:	<http://www.europeana.eu/schemas/edm/>

SELECT ?object ?title ?dataset ?startDate ?endDate ?thumb ?ref ?manifest ?service ?match ?model ?modelFormat ?modelPublisher WHERE {
?object a crm:E22_Man-Made_Object ;
        crm:P1_is_identified_by ?id1 ;
        void:inDataset ?dataset .
?id1 a crm:E33_E41_Linguistic_Appellation ;
    crm:P190_has_symbolic_content ?title .
OPTIONAL { ?object foaf:thumbnail ?thumb }
OPTIONAL { ?object foaf:depiction ?ref 
	OPTIONAL { ?ref dcterms:isReferencedBy ?manifest ;
		svcs:has_service ?service}}
OPTIONAL { ?object edm:isShownBy ?model .
?model dcterms:format ?modelFormat ;
	dcterms:publisher ?modelPublisher}
?object crm:P108i_was_produced_by/crm:P7_took_place_at ?place .
{?place skos:exactMatch ?match FILTER strStarts(str(?match), "https://pleiades")}
UNION {?place^skos:exactMatch ?kid .
  ?kid skos:inScheme kid: ;
       skos:exactMatch ?match FILTER strStarts(str(?match), "https://pleiades")}
OPTIONAL {?object crm:P108i_was_produced_by/crm:P4_has_time-span ?timeSpan .
?timeSpan crm:P82a_begin_of_the_begin ?startDate .
?timeSpan crm:P82b_end_of_the_end ?endDate}
} LIMIT %LIMIT% OFFSET %OFFSET%